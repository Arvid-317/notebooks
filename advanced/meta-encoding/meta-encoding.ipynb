{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Problem Description.\n",
    "\n",
    "This project is based in the section about Meta programming of the paper [1].\n",
    "Before starting, we recommend you to read carefully that section, \n",
    "but you can skip the subsection on Guess-and-check.\n",
    "\n",
    "We start with some preliminaries, and afterwards we describe the parts of this project.\n",
    "\n",
    "[1] Kaminski, R., Romero, J., Schaub, T., & Wanko, P. (2020). How to build your own ASP-based system?! CoRR, abs/2008.06692."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Preliminaries.\n",
    "\n",
    "Let us consider the Latin Square problem:\n",
    "given a quadratic board of size `s`, \n",
    "the goal is to fill each cell of the\n",
    "board with some number from `1` to `s` such that \n",
    "no number occurs twice in the same row or column.\n",
    "\n",
    "This problem can be represented by the following logic program:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Writing latin.lp\n"
     ]
    }
   ],
   "source": [
    "%%file latin.lp\n",
    "number(1..s).\n",
    "\n",
    "1 { latin(X,Y,N) : number(N) } 1 :- number(X), number(Y).\n",
    "1 { latin(X,Y,N) : number(Y) } 1 :- number(X), number(N).\n",
    "1 { latin(X,Y,N) : number(X) } 1 :- number(Y), number(N).\n",
    "\n",
    "#show latin/3."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The paper [1] presents the following meta encoding `meta.lp`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Overwriting meta.lp\n"
     ]
    }
   ],
   "source": [
    "%%file meta.lp\n",
    "\n",
    "conjunction(B) :- literal_tuple(B),\n",
    "        hold(L) : literal_tuple(B, L), L > 0;\n",
    "    not hold(L) : literal_tuple(B,-L), L > 0.\n",
    "\n",
    "body(normal(B)) :- rule(_,normal(B)), conjunction(B).\n",
    "body(sum(B,G))  :- rule(_,sum(B,G)),\n",
    "    #sum { W,L :     hold(L), weighted_literal_tuple(B, L,W), L > 0 ;\n",
    "           W,L : not hold(L), weighted_literal_tuple(B,-L,W), L > 0 } >= G.\n",
    "\n",
    "  hold(A) : atom_tuple(H,A)   :- rule(disjunction(H),B), body(B).\n",
    "{ hold(A) : atom_tuple(H,A) } :- rule(     choice(H),B), body(B).\n",
    "\n",
    "#show.\n",
    "#show T : output(T,B), conjunction(B)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The file can be used to compute answer sets of a logic program `P`\n",
    "given a reified version of the program `P` generated by `clingo`.\n",
    "For example, the answer sets generated by this call:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "clingo version 5.4.0\r\n",
      "Reading from latin.lp\r\n",
      "Solving...\r\n",
      "SATISFIABLE\r\n",
      "\r\n",
      "Models       : 576\r\n",
      "Calls        : 1\r\n",
      "Time         : 0.004s (Solving: 0.00s 1st Model: 0.00s Unsat: 0.00s)\r\n",
      "CPU Time     : 0.004s\r\n"
     ]
    }
   ],
   "source": [
    "!clingo latin.lp -c s=4 0 --quiet"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "are the same as those generated by this one:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "clingo version 5.4.0\r\n",
      "Reading from reify.lp ...\r\n",
      "Solving...\r\n",
      "SATISFIABLE\r\n",
      "\r\n",
      "Models       : 576\r\n",
      "Calls        : 1\r\n",
      "Time         : 0.030s (Solving: 0.00s 1st Model: 0.00s Unsat: 0.00s)\r\n",
      "CPU Time     : 0.030s\r\n"
     ]
    }
   ],
   "source": [
    "!clingo latin.lp -c s=4 --output=reify > reify.lp\n",
    "!clingo reify.lp meta.lp 0 --quiet"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "You can check this equivalence using the following Python function.\n",
    "It takes as input a string and some files that, together, specify a logic program.\n",
    "The function returns a sorted version of the answer sets of the program."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [],
   "source": [
    "import clingo\n",
    "\n",
    "def run(program=\"\", files=[]):\n",
    "    ctl = clingo.Control([\"0\"])\n",
    "    ctl.add(\"base\", [], program)\n",
    "    for f in files:\n",
    "        ctl.load(f)\n",
    "    ctl.ground([(\"base\", [])])\n",
    "    with ctl.solve(yield_=True) as handle:\n",
    "        return sorted(sorted(m.symbols(shown=True) for m in handle))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Note that the answer sets returned consist only of atoms and terms that are shown.\n",
    "\n",
    "We can check that the previous programs have the same answer sets as follows:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "!clingo latin.lp -c s=4 --output=reify > reify.lp\n",
    "run(\"\", ['reify.lp', 'meta.lp']) == run(\"#const s=4.\", ['latin.lp'])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The comparison returns `True` because the answer sets of both program are the same."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Part 1. From clingo logic programs to disjunctive logic programs\n",
    "\n",
    "The task of this part is to write a meta encoding `meta-disjunctive.lp` that is similar to `meta.lp`,\n",
    "but after grounding using `clingo` it generates a disjunctive logic program (without choice rules or weight rules).\n",
    "The meta encoding may contain some aggregates of cardinality constraints in the bodies, \n",
    "but they should be completely evaluated by the grounder of `clingo`.\n",
    "\n",
    "The new meta encoding `meta-disjunctive.lp` should behave like `meta.lp`, so that\n",
    "it can be used to compute answer sets of a logic program `P`\n",
    "given a reified version of the program `P` generated by `clingo`.\n",
    "You can run the same test as before:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "!clingo latin.lp -c s=4 --output=reify > reify.lp\n",
    "run(\"\", ['reify.lp', 'meta-disjunctive.lp']) == run(\"\", ['latin.lp'])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "You can check if the meta encoding generates no ground choice rules or weight rules with this call:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "!clingo latin.lp              -c s=4 --output=reify > reify.lp\n",
    "!clingo reify.lp meta-disjunctive.lp --output=reify"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "If the meta encoding is correct, \n",
    "then the output should contain no facts of predicate `weighted_literal_tuple/3`, \n",
    "and no facts of the form `rule(choice(t1),t2)` for any terms `t1` and `t2`."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Translating choice rules\n",
    "\n",
    "The meta-encoding `meta.lp` contains the rule\n",
    "```\n",
    "{ hold(A) : atom_tuple(H,A) } :- rule(     choice(H),B), body(B).\n",
    "```\n",
    "that generates the atoms of a choice rule whenever its body holds.\n",
    "The first task of this part is to replace that rule by a set of normal rules.\n",
    "\n",
    "In our course Answer Set Solving in Practice (https://teaching.potassco.org), \n",
    "in the Language part, we explain how to do this for propositional logic programs.\n",
    "You can apply the same approach here."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Translating weight rules\n",
    "\n",
    "The meta-encoding `meta.lp` contains the rule\n",
    "```\n",
    "body(sum(B,G))  :- rule(_,sum(B,G)),\n",
    "    #sum { W,L :     hold(L), weighted_literal_tuple(B, L,W), L > 0 ;\n",
    "           W,L : not hold(L), weighted_literal_tuple(B,-L,W), L > 0 } >= G.\n",
    "```\n",
    "that determines when the body of a weight rule holds, using a `#sum` aggregate in the body.\n",
    "The second task of this part is to replace that rule by a set of normal rules.\n",
    "These rules should not contain aggregates or cardinality constraints, \n",
    "unless they are completely evaluated by the grounder of `clingo`.\n",
    "\n",
    "In our course Answer Set Solving in Practice (teaching.potassco.org),\n",
    "in the Language part, \n",
    "we explain how to do this for cardinality rules occurring in propositional logic programs.\n",
    "You can apply the same approach here, except that in this case, \n",
    "whenever a literal holds, instead of adding `1` to the accumulated value `k`, \n",
    "we have to add the corresponding weight `w` of the literal.\n",
    "\n",
    "Note that the weights `w` generated by `clingo` in the reified output are always positive integers.\n",
    "In other words, the third argument of the atoms of the predicate `weighted_literal_tuple/3` is always a positive integer.\n",
    "\n",
    "It may be helpful to order the literals occurring in the weight rules.\n",
    "You can do it with this rule:\n",
    "```\n",
    "weighted_literal_tuple(B,L,W,P+1) :- weighted_literal_tuple(B,L,W),\n",
    "                                     P = #count{ LL,WW : weighted_literal_tuple(B,LL,WW), (LL,WW)<(L,W) }. \n",
    "```\n",
    "Observe that it uses an aggregate, but since it only depends on `weighted_literal_tuple/3`, \n",
    "it can be compeltely evaluated by the grounder of `clingo`.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Part 2. From tight normal programs to programs with choice rules and constraints.\n",
    "\n",
    "In this part we restrict ourselves to normal programs with integrity constraints.\n",
    "These programs do not contain disjunctions, choice rules or weight rules. \n",
    "Furthermore, we restrict ourselves to tight programs, i.e., \n",
    "programs `P` such that the positive atom dependency graph of `ground(P)` is acyclic. \n",
    "\n",
    "Note that these are the programs that we obtain from Part 1\n",
    "if the original program is tight and non-disjunctive, \n",
    "as in the case of `latin.lp`.\n",
    "To see this, please inspect the output of these calls:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "!clingo latin.lp              -c s=4 --output=reify > reify.lp\n",
    "!clingo reify.lp meta-disjunctive.lp --output=reify"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The task of this part is to write a meta encoding `meta-constraints.lp` that is similar to `meta.lp`,\n",
    "but only consists of choices rules and constraints.\n",
    "The new meta encoding `meta-constraints.lp` should behave like `meta.lp`, so that\n",
    "it can be used to compute answer sets of a tight normal logic program `P` with integrity constraints,\n",
    "given a reified version of the program `P` generated by `clingo`.\n",
    "You can run a similar same test as before:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "!clingo latin.lp               -c s=4 --output=reify > reify1.lp\n",
    "!clingo reify1.lp meta-disjunctive.lp --output=reify > reify2.lp\n",
    "run(\"\", ['reify2.lp', 'meta-constraints.lp']) == run(\"\", ['latin.lp'])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In the course Answer Set Solving in Practice (teaching.potassco.org), \n",
    "in the Solving part, \n",
    "we show how to represent a tight logic program by a set of nogoods\n",
    "such that the answer sets of the logic program correspond \n",
    "to the solutions of the nogoods.\n",
    "For tight logic programs, those nogoods are called the completion nogoods.\n",
    "\n",
    "You can use this result to translate tight normal logic programs `P` with integrity constraints,\n",
    "to programs `P'`with choice rules and constraints.\n",
    "The choice rules are used to generate all possible assignments, while\n",
    "the constraints are used to represent the nogoods.\n",
    "For example, the nogood `{Ta, Fb, Fc}` can be represented by the constraint `:- a, not b, not c.`.\n",
    "In this way, the answer sets of `P'` correspond to the solutions of the set of completion nogoods, \n",
    "that, given the previous result from the course, correspond to the answer sets of `P`.\n",
    "\n",
    "We already provide you with the choice rules and the show statements that you need:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Writing meta-constraints.lp\n"
     ]
    }
   ],
   "source": [
    "%%file meta-constraints.lp\n",
    "\n",
    "{conjunction(B)} :- literal_tuple(B).\n",
    "{     hold(|L|)} :- literal_tuple(B,L).\n",
    "{     hold(|A|)} :- atom_tuple(H,A).\n",
    "\n",
    "#show.\n",
    "#show T : output(T,B), conjunction(B)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Your task is to add the constraints representing the nogoods.\n",
    "You only need two constraints for the atom-oriented nogoods, and\n",
    "three constraints for the body-oriented nogoods."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
